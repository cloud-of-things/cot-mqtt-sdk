plugins {
    id 'java'
    id 'application'
    id "net.linguica.maven-settings" version "0.5"
    id 'maven'
    id 'net.researchgate.release' version '2.6.0'
}

ext {
    vertxVersion = '3.5.1'
    bouncycastleVersion = '1.58'
    findbugsVersion = '1.3.9'
    junitVersion = "4.12"
}

sourceSets {
    systemTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/system-test/java')
        }
        resources.srcDir file('src/system-test/resources')
    }
}

compileJava {
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

configurations {
    systemTestCompile.extendsFrom testCompile
    systemTestRuntime.extendsFrom testRuntime
}

task systemTest(type: Test) {
    testClassesDirs = sourceSets.systemTest.output.classesDirs
    classpath = sourceSets.systemTest.runtimeClasspath
}

repositories {
    mavenCentral()
    mavenLocal()
    jcenter()
    maven { url "https://plugins.gradle.org/m2/" }
    maven { url "https://infinity-wbench.wesp.telekom.net/nexus/content/repositories/public" }
    maven { url "https://infinity-wbench.wesp.telekom.net/nexus/content/repositories/2ndparty" }
    maven { url "https://infinity-wbench.wesp.telekom.net/nexus/content/repositories/snapshots" }
    maven { url "https://infinity-wbench.wesp.telekom.net/nexus/content/repositories/nbiot-releases" }
    maven { url "https://infinity-wbench.wesp.telekom.net/nexus/content/repositories/nbiot-snapshots" }
}

group = 'de.tarent.telekom.cot'
version = '0.5.0-SNAPSHOT'
sourceCompatibility = '1.8'

dependencies {
    systemTestCompile sourceSets.main.output
    systemTestCompile sourceSets.test.output

    systemTestCompile configurations.compile
    systemTestCompile configurations.testCompile

    systemTestRuntime configurations.runtime
    systemTestRuntime configurations.testRuntime

    compile "io.vertx:vertx-core:$vertxVersion"
    compile "io.vertx:vertx-mqtt:$vertxVersion"
    compile "io.vertx:vertx-config:$vertxVersion"
    compile "org.bouncycastle:bcprov-jdk15on:$bouncycastleVersion"
    compile "com.google.code.findbugs:jsr305:$findbugsVersion"

    testCompile "io.vertx:vertx-unit:$vertxVersion"
    testCompile "junit:junit:$junitVersion"
}

mainClassName = 'de.tarent.telekom.cot.mqtt.MQTTHelper'

run {
    args = ['run', mainClassName, "--launcher-class=io.vertx.core.Launcher"]
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.0'
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(
                    url: "${wBenchNexusUrl}/content/repositories/nbiot-releases") {
                authentication(userName: wBenchNexusUser, password: wBenchNexusPw)
            }
            snapshotRepository(
                    url: "${wBenchNexusUrl}/content/repositories/nbiot-snapshots") {
                authentication(userName: wBenchNexusUser, password: wBenchNexusPw)
            }
        }
    }
}