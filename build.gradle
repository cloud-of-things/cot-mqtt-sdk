buildscript {
    repositories {
        maven { url "${wBenchNexusUrl}/content/repositories/gradle.org" }
        mavenCentral()
        mavenLocal()
        jcenter()
        maven { url "${wBenchNexusUrl}/content/repositories/public" }
        maven { url "${wBenchNexusUrl}/content/repositories/2ndparty" }
        maven { url "${wBenchNexusUrl}/content/repositories/snapshots" }
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.6.2"
        classpath "net.linguica.gradle:maven-settings-plugin:0.5"
    }
}

apply plugin: "java"
apply plugin: "application"
apply plugin: "net.linguica.maven-settings"
apply plugin: "maven-publish"
apply plugin: "org.sonarqube"
apply plugin: "jacoco"

ext {
    vertxVersion = '3.5.1'
    bouncycastleVersion = '1.58'
    findbugsVersion = '1.3.9'
    junitVersion = "4.12"
}

sourceSets {
    systemTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/system-test/java')
        }
        resources.srcDir file('src/system-test/resources')
    }
}

compileJava {
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

configurations {
    systemTestCompile.extendsFrom testCompile
    systemTestRuntime.extendsFrom testRuntime
}

task systemTest(type: Test) {
    testClassesDirs = sourceSets.systemTest.output.classesDirs
    classpath = sourceSets.systemTest.runtimeClasspath
}

repositories {
    mavenCentral()
    mavenLocal()
    jcenter()
    maven { url "${wBenchNexusUrl}/content/repositories/public" }
    maven { url "${wBenchNexusUrl}/content/repositories/2ndparty" }
    maven { url "${wBenchNexusUrl}/content/repositories/snapshots" }
    maven { url "${wBenchNexusUrl}/content/repositories/gradle.org" }
    maven {
        name "nbiot-releases"
        url "${wBenchNexusUrl}/content/repositories/nbiot-releases"
    }
    maven {
        name "nbiot-snapshots"
        url "${wBenchNexusUrl}/content/repositories/nbiot-snapshots"
    }
}

group = 'de.tarent.telekom.cot'
version = nbiotMqttVersion // Don't forget to change this in the Readme.md!
sourceCompatibility = '1.8'

dependencies {
    systemTestCompile sourceSets.main.output
    systemTestCompile sourceSets.test.output

    systemTestCompile configurations.compile
    systemTestCompile configurations.testCompile

    systemTestRuntime configurations.runtime
    systemTestRuntime configurations.testRuntime

    compile "io.vertx:vertx-core:$vertxVersion"
    compile "io.vertx:vertx-mqtt:$vertxVersion"
    compile "io.vertx:vertx-config:$vertxVersion"
    compile "org.bouncycastle:bcprov-jdk15on:$bouncycastleVersion"
    compile "com.google.code.findbugs:jsr305:$findbugsVersion"

    testCompile "io.vertx:vertx-unit:$vertxVersion"
    testCompile "junit:junit:$junitVersion"
}

mainClassName = 'de.tarent.telekom.cot.mqtt.MQTTHelper'

run {
    args = ['run', mainClassName, "--launcher-class=io.vertx.core.Launcher"]
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.0'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact packageSources
            artifact packageJavadoc
        }
    }
    repositories {
        maven {
            name "${project.version.endsWith('-SNAPSHOT') ? 'nbiot-snapshots':'nbiot-releases'}"
            url "${wBenchNexusUrl}/content/repositories/${project.version.endsWith('-SNAPSHOT') ? 'nbiot-snapshots':'nbiot-releases'}"
        }
    }
}

task packageJavadoc(type: Jar) {
    from javadoc
    classifier = 'javadoc'
}
task packageSources(type: Jar, dependsOn: 'classes') {
    from sourceSets.main.allSource
    classifier = 'sources'
}
artifacts {
    archives packageJavadoc
    archives packageSources
}
